plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.sonarqube" version "7.0.1.6134"
    id "jacoco"
}

group = 'com.cs203'
version = '0.0.1-SNAPSHOT'
description = 'Tariff Calculator'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
    maven {
        name = 'Central Portal Snapshots'
        url = 'https://central.sonatype.com/repository/maven-snapshots/'
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
        showStandardStreams = true
        showStackTraces = true
    }
    finalizedBy jacocoTestReport // report is always generated after tests run
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'com.nimbusds:nimbus-jose-jwt'
    implementation 'org.springframework.security:spring-security-oauth2-client'
    implementation 'org.springframework.security:spring-security-oauth2-authorization-server'
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.37'
    // https://mvnrepository.com/artifact/org.springdoc/springdoc-openapi-starter-webmvc-ui
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'
    implementation platform("org.springframework.ai:spring-ai-bom:1.0.3")
    implementation 'org.springframework.ai:spring-ai-starter-model-openai'
}

tasks.named('test') {
    useJUnitPlatform()
}

sonar {
  properties {
    property "sonar.projectKey", "TheMajoris_TARIFF"
    property "sonar.organization", "majoris"
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.sources", "src/main/java"
    property "sonar.tests", "src/test/java"
    property "sonar.java.binaries", "build/classes/java/main"
    property "sonar.java.test.binaries", "build/classes/java/test"
    property "sonar.sourceEncoding", "UTF-8"
    property "sonar.exclusions", "**/generated/**,**/build/**"
  }
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "**/config/*",
                    "**/dto/*",
                    "**/enums/*",
                    "**/mapper/*"
            ])
        }))
    }

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
}
